# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=srt
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.3.1
pkgrel=3
pkgdesc="Secure Reliable Transport (SRT) (mingw-w64)"
arch=('any')
license=('MPLv2.0')
url="https://www.srtalliance.org/"
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs"
         "${MINGW_PACKAGE_PREFIX}-libwinpthread"
         "${MINGW_PACKAGE_PREFIX}-openssl")
makedepends=("${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-gcc"
             "${MINGW_PACKAGE_PREFIX}-pkg-config"
             "make")
source=("${_realname}-${pkgver}.tar.gz"::"https://github.com/Haivision/srt/archive/v${pkgver}.tar.gz"
        0001-CMakeLists.txt-substitute-link-flags-for-package-nam.patch
        0003-add-implicit-link-libraries.patch
        0004-mingw-ws2_32-linking.patch
        0005-mingw-w64-pthread.patch
        0006-no-msvc-compat-headers.patch)
sha256sums=('f202801d9e53cd8854fccc1ca010272076c32c318396c8f61fb9a61807c3dbea'
            'e127fcc9238e8947be9df3a95ed6c34db09cdb6177bc5087906e3d04fef96c07'
            'ea35b32907f264eab70a88537df76b54409304366050b0f62e3e778fc5fe47cf'
            '1dc63f94713c04cef613b37d1f31ccff3c685e431a6a73027045c4d158c3c0d9'
            'fd1436b098844bcebae27dc0584a3b332226648abc3edcc57e503b62e18625bb'
            '8468dde4a75f277a03fdc05537bdca201b93d03669beccc588da6c2b6bddb0c2')
noextract=(${_realname}-${pkgver}.tar.gz)

prepare() {
  [[ -d ${srcdir}/${_realname}-${pkgver} ]] && rm -rf ${srcdir}/${_realname}-${pkgver}
  tar -xzf ${srcdir}/${_realname}-${pkgver}.tar.gz -C ${srcdir} || true

  cd ${_realname}-${pkgver}
  patch -p1 -i ${srcdir}/0001-CMakeLists.txt-substitute-link-flags-for-package-nam.patch
  patch -p1 -i ${srcdir}/0003-add-implicit-link-libraries.patch
  patch -p1 -i ${srcdir}/0004-mingw-ws2_32-linking.patch
  patch -p1 -i ${srcdir}/0005-mingw-w64-pthread.patch
  patch -p1 -i ${srcdir}/0006-no-msvc-compat-headers.patch
}

build() {
  [[ -d "${srcdir}"/build-${MINGW_CHOST} ]] && rm -rf "${srcdir}"/build-${MINGW_CHOST}
  mkdir -p ${srcdir}/build-${MINGW_CHOST}
  cd ${srcdir}/build-${MINGW_CHOST}
  
  CXXFLAGS+=" -D_WIN32_WINNT=0x600"
  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
    ${MINGW_PREFIX}/bin/cmake \
      -G'MSYS Makefiles' \
      -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
      -DCMAKE_BUILD_TYPE=Release \
      -DENABLE_SHARED=ON \
      -DENABLE_STATIC=ON \
      ../${_realname}-${pkgver}

  make
}

package() {
  # Shared Install
  cd "${srcdir}/build-${MINGW_CHOST}"
  make DESTDIR=${pkgdir} install

  install -Dm644 ${srcdir}/${_realname}-${pkgver}/LICENSE ${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE

  local _WIN_PREFIX=$(cygpath -am ${MINGW_PREFIX})
  for _f in "${pkgdir}${MINGW_PREFIX}"/lib/pkgconfig/*.pc; do
    sed -e "s|${_WIN_PREFIX}|${MINGW_PREFIX}|g" -i ${_f}
  done
}
