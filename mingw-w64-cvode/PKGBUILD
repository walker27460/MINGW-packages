# Maintainer: Francis Giraldeau <francis.giraldeau@nrc-cnrc.gc.ca>

_realname=cvode
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=3.2.1
pkgrel=1
pkgdesc="ordinary differential equation system solver (mingw-w64)"
arch=('any')
url="https://computation.llnl.gov/projects/sundials"
license=('BSD')
makedepends=("${MINGW_PACKAGE_PREFIX}-cmake")
source=("${_realname}-${pkgver}.tar.gz"::https://computation.llnl.gov/projects/sundials/download/${_realname}-${pkgver}.tar.gz
        001-install-location.patch)
sha256sums=('aa4f5f3c5114e93ba246630cf107b1f73de5c1028a013602c114d3cde6564451'
            'aea097dc7c040161c67d0588147ff2aa4f2ae170c32d3cf8b246c28b71c39109')

prepare() {
  cd ${_realname}-${pkgver}
  patch -p1 -i ${srcdir}/001-install-location.patch
}

build() {
  [[ -d build-${MINGW_CHOST} ]] && rm -rf build-${MINGW_CHOST}
  mkdir -p build-${MINGW_CHOST}
  cd build-${MINGW_CHOST}

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
  ${MINGW_PREFIX}/bin/cmake.exe \
    -G"MSYS Makefiles" \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
    ../${_realname}-${pkgver}

  make
}

package() {
  cd build-${MINGW_CHOST}
  make DESTDIR=${pkgdir} install

  # license
  rm ${pkgdir}${MINGW_PREFIX}/include/sundials/LICENSE
  install -D -m644 ${srcdir}/${_realname}-${pkgver}/LICENSE \
  ${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE
}
